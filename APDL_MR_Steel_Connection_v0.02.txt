/Clear
/PREP7
!ET,1,45
ET,1,185
ET,2,TARGE170
ET,3,CONTA174 
MP,EX,1,210000		!Material Properties, Young's Modulus, Material 1, 210000 MPa
MP,PRXY,1,0.3		!Poisson
MP,MU,1,0.3			!MIU
K,1,0,0,0
pi=3.14159

!Loading force [Newtons]
Q=50000

TYPE,1

!Column dimensions [mm]
st_inaltime=1000  !length of the analysed member
st_bf=300 !flange width
st_tf=20 !flange thickness
st_hw=300 !web height
st_tw=10 !web thikness

element=10 !Mesh size

!Beam dimensions[mm]
gr_lungime=1000 !length of the analysed member
cota_inf=300 !Position of attachment to the column, refers to the lower flange distance up to the ground
gr_bf=200 !flange width
gr_tf=20 !flange thickness
gr_hw=300 !web height
gr_tw=10 !web thikness

!End plate geometry [mm]
f_h=500 !height
f_w=300 !width
f_t=20 !thickness

!Bolted connection geometry
r_bulon = 8 !bolts radius in mm
r_gaura = 9 !hole radius
r_piulita = 15 !radius of bolt head
grosime_piulita = 10 !thickness of the bolt head
d_inima=40	!distance from the web to the bolt line (horizontal direction)
z1= cota_inf-40   !Spacing between the vertical lines of bolts (relative to the attachement height )
z2= cota_inf + 80
z3= cota_inf + 150
z4= cota_inf + 200
z5= cota_inf + 250

!Creation of the column's flanges

K,2,0,st_bf/2,0
K,3,-st_tf,st_bf/2,0
K,4,-st_tf,-st_bf/2,0
K,5,0,-st_bf/2,0

K,6,0,st_bf/2,st_inaltime
K,7,-st_tf,st_bf/2,st_inaltime
K,8,-st_tf,-st_bf/2,st_inaltime
K,9,0,-st_bf/2,st_inaltime

V,2,3,4,5,6,7,8,9

K,10,-st_hw-st_tf,st_bf/2,0
K,11,-st_hw-st_tf-st_tf,st_bf/2,0
K,12,-st_hw-st_tf-st_tf,-st_bf/2,0
K,13,-st_hw-st_tf,-st_bf/2,0

K,14,-st_hw-st_tf,st_bf/2,st_inaltime
K,15,-st_hw-st_tf-st_tf,st_bf/2,st_inaltime
K,16,-st_hw-st_tf-st_tf,-st_bf/2,st_inaltime
K,17,-st_hw-st_tf,-st_bf/2,st_inaltime

V,10,11,12,13,14,15,16,17

!Creation of the column's web

K,18,-st_tf,st_tw/2,0
K,19,-st_hw-st_tf,st_tw/2,0
K,20,-st_hw-st_tf,-st_tw/2,0
K,21,-st_tf,-st_tw/2,0

K,22,-st_tf,st_tw/2,st_inaltime
K,23,-st_hw-st_tf,st_tw/2,st_inaltime
K,24,-st_hw-st_tf,-st_tw/2,st_inaltime
K,25,-st_tf,-st_tw/2,st_inaltime

V,18,19,20,21,22,23,24,25



!Creation of the beam's flanges
K,26,f_t,-gr_bf/2,cota_inf
K,27,f_t,gr_bf/2,cota_inf
K,28,f_t,gr_bf/2,cota_inf+gr_tf
K,29,f_t,-gr_bf/2,cota_inf+gr_tf

K,30,f_t+gr_lungime,-gr_bf/2,cota_inf
K,31,f_t+gr_lungime,gr_bf/2,cota_inf
K,32,f_t+gr_lungime,gr_bf/2,cota_inf+gr_tf
K,33,f_t+gr_lungime,-gr_bf/2,cota_inf+gr_tf

V,26,27,28,29,30,31,32,33

K,34,f_t,-gr_bf/2,cota_inf+gr_tf+gr_hw
K,35,f_t,gr_bf/2,cota_inf+gr_tf+gr_hw
K,36,f_t,gr_bf/2,cota_inf+gr_tf+gr_tf+gr_hw
K,37,f_t,-gr_bf/2,cota_inf+gr_tf+gr_tf+gr_hw

K,38,f_t+gr_lungime,-gr_bf/2,cota_inf+gr_tf+gr_hw
K,39,f_t+gr_lungime,gr_bf/2,cota_inf+gr_tf+gr_hw
K,40,f_t+gr_lungime,gr_bf/2,cota_inf+gr_tf+gr_tf+gr_hw
K,41,f_t+gr_lungime,-gr_bf/2,cota_inf+gr_tf+gr_tf+gr_hw

V,34,35,36,37,38,39,40,41

!Creation of the beam's web

K,42, f_t , -gr_tw/2, cota_inf+gr_tf
K,43, f_t , gr_tw/2, cota_inf+gr_tf
K,44, f_t , gr_tw/2, cota_inf+gr_tf + gr_hw
K,45, f_t , -gr_tw/2, cota_inf+gr_tf + gr_hw

K,46, f_t+gr_lungime , -gr_tw/2, cota_inf+gr_tf
K,47, f_t+gr_lungime , gr_tw/2, cota_inf+gr_tf
K,48, f_t+gr_lungime , gr_tw/2, cota_inf+gr_tf + gr_hw
K,49, f_t+gr_lungime , -gr_tw/2, cota_inf+gr_tf + gr_hw

V,42,43,44,45,46,47,48,49

!Creation of the beam's endplate

K,50, 0 , -f_w/2 , cota_inf + gr_tf + gr_hw/2 - f_h/2
K,51, 0 , f_w/2 , cota_inf + gr_tf + gr_hw/2 - f_h/2
K,52, 0 , f_w/2 , cota_inf + gr_tf + gr_hw/2 + f_h/2
K,53, 0 , -f_w/2 , cota_inf + gr_tf + gr_hw/2 + f_h/2

K,54, f_t , -f_w/2 , cota_inf + gr_tf + gr_hw/2 - f_h/2
K,55, f_t , f_w/2 , cota_inf + gr_tf + gr_hw/2 - f_h/2
K,56, f_t , f_w/2 , cota_inf + gr_tf + gr_hw/2 + f_h/2
K,57, f_t , -f_w/2 , cota_inf + gr_tf + gr_hw/2 + f_h/2

V,50,51,52,53,54,55,56,57

!Creation of the bolted connection
WPLANE, ,0,0,0,0,0,1,0,1,0

!Creation of the holes

CYL4, z1 , d_inima, r_gaura, , , , st_tf + f_t
CYL4, z1 , -d_inima, r_gaura, , , , st_tf + f_t

CYL4, z2 , d_inima, r_gaura, , , , st_tf + f_t
CYL4, z2 , -d_inima, r_gaura, , , , st_tf + f_t

CYL4, z3 , d_inima, r_gaura, , , , st_tf + f_t
CYL4, z3 , -d_inima, r_gaura, , , , st_tf + f_t

CYL4, z4 , d_inima, r_gaura, , , , st_tf + f_t
CYL4, z4 , -d_inima, r_gaura, , , , st_tf + f_t

CYL4, z5 , d_inima, r_gaura, , , , st_tf + f_t
CYL4, z5 , -d_inima, r_gaura, , , , st_tf + f_t

VGEN, , 8,17 ,1,st_tf, , , , ,1	!moves cylinder

VSBV, all, 8, , , DELETE	!creates hole
VSBV, all, 9, , , DELETE	!creates hole
VSBV, all, 10, , , DELETE	!creates hole
VSBV, all, 11, , , DELETE	!creates hole
VSBV, all, 12, , , DELETE	!creates hole
VSBV, all, 13, , , DELETE	!creates hole
VSBV, all, 14, , , DELETE	!creates hole
VSBV, all, 15, , , DELETE	!creates hole
VSBV, all, 16, , , DELETE	!creates hole
VSBV, all, 17, , , DELETE	!creates hole

!Creation of the first bolts
CYL4, z1 , d_inima, r_bulon, , , , st_tf + f_t
VGEN, , 8, ,,st_tf, , , , ,1	!moves cylinder

CYL4, z1 , d_inima, r_piulita, , , , grosime_piulita
VGEN, , 9, ,,st_tf + grosime_piulita, , , , ,1	!moves cylinder

CYL4, z1 , d_inima, r_piulita, , , , grosime_piulita
VGEN, , 10, ,,-st_tf, , , , ,1	!moves cylinder

VADD,8,9,10

!Creating bolts copies for the other rows

VGEN, 2, 11, , , 0, 0, z2-z1, 0, 0, 0
VGEN, 2, 11, , , 0, 0, z3-z1, 0, 0, 0
VGEN, 2, 11, , , 0, 0, z4-z1, 0, 0, 0
VGEN, 2, 11, , , 0, 0, z5-z1, 0, 0, 0

VGEN,2 , 8,12 ,1,,-2*d_inima , , , ,	!copies on the bolts on the other side

!Joining of the volumes for the beam and column (lower flange+ web +upper flange)
VADD,1,2,3
VADD,4,5,6,7

!MESH

!SMRTSIZE,4
!VMESH,ALL

ALLS
ESIZE,element
MSHAPE,1,3D 
MSHKEY,0
VSEL, U, , , 8, 17, 1, 
VMESH,ALL

ESIZE,element/4
VSEL, S, , , 8, 17, 1, 
VMESH,ALL

WPCSYS,,

NSEL,S,LOC,Z,st_inaltime
NSEL,A,LOC,z,0
D,ALL,ALL,0
ALLS


NSEL,S,LOC,Z,cota_inf+2*gr_tf+gr_hw
NSEL,R,LOC,X, f_t+1 , f_t+gr_lungime

*Get,NRNODURI,node,0,count !Get the number of nodes in the selected set
F,ALL,FZ,-Q/NRNODURI

ALLS
NSEL,S,LOC,Z,cota_inf+2*gr_tf+gr_hw
NSEL,R,LOC,X, f_t+1 , f_t+gr_lungime
D,ALL,UY,0
!D,ALL,UZ,0

!Modeling contact between beam-bolts-column
!Column - Bolts
TYPE,2 !TARGET
R,1, , ,
REAL,1
VSEL,S,,,18
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, -st_tf
NSEL, R, LOC, Y, d_inima-r_piulita-5,d_inima+r_piulita+5
ESURF

TYPE,3
VSEL,S,,,8,17,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, -st_tf
NSEL, R, LOC, Y, d_inima-r_piulita-5,d_inima+r_piulita+5
ESURF


TYPE,2 !TARGET
R,2, , ,
REAL,2
VSEL,S,,,18
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, -st_tf
NSEL, R, LOC, Y, -d_inima-r_piulita-5,-d_inima+r_piulita+5
ESURF

TYPE,3
VSEL,S,,,8,17,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, -st_tf
NSEL, R, LOC, Y, -d_inima-r_piulita-5,-d_inima+r_piulita+5
ESURF

!Beam - Bolts
TYPE,2 !TARGET
R,3, , ,
REAL,3
VSEL,S,,,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, f_t
NSEL, R, LOC, Y, d_inima-r_piulita-5,d_inima+r_piulita+5
ESURF

TYPE,3
VSEL,S,,,8,17,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, f_t
NSEL, R, LOC, Y, d_inima-r_piulita-5,d_inima+r_piulita+5
ESURF

TYPE,2 !TARGET
R,4 , ,
REAL,4
VSEL,S,,,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, f_t
NSEL, R, LOC, Y, -d_inima-r_piulita-5,-d_inima+r_piulita+5
ESURF

TYPE,3
VSEL,S,,,8,17,1
NSLV,S,1
NSEL, R, LOC, Z, z1-r_piulita-5,z1+r_piulita+5
NSEL, A, LOC, Z, z2-r_piulita-5,z2+r_piulita+5
NSEL, A, LOC, Z, z3-r_piulita-5,z3+r_piulita+5
NSEL, A, LOC, Z, z4-r_piulita-5,z4+r_piulita+5
NSEL, A, LOC, Z, z5-r_piulita-5,z5+r_piulita+5
NSEL, R, LOC, X, f_t
NSEL, R, LOC, Y, -d_inima-r_piulita-5,-d_inima+r_piulita+5
ESURF

!Contact between beam endplate and column flange
TYPE,2
R,5, , ,
REAL,5
VSEL,S,,,18
NSLV,S,1
NSEL, R, LOC, X, 0
NSEL, R, LOC, Z, cota_inf + gr_tf + gr_hw/2 - f_h/2-20, cota_inf + gr_tf + gr_hw/2 + f_h/2+20,1
ESURF

TYPE,3
VSEL,S,,,1
NSLV,S,1
NSEL, R, LOC, X, 0
NSEL, R, LOC, Z, cota_inf-20, cota_inf+gr_tf*2+gr_hw+20,1
ESURF


!contact along the bolt length
TYPE,2
R,6,,,
REAL,6
VSEL,S,,,18
ASLV
ASEL, R, LOC, X, -st_tf+1, -1, 1, 0
ASEL, R, LOC, Y, -d_inima-r_bulon-5 , d_inima+r_bulon+5, 1, 0
ASEL, R, LOC, Z, z1-5 , z5+5, 1, 0
NSLA,,1
ESURF

TYPE,3
VSEL,S,,,8,17,1
ASLV
NSLA,,1
NSEL,R,LOC,X,-st_tf+1,f_t-1,1
ESURF

TYPE,2
R,7,,,
REAL,7
VSEL,S,,,1
ASLV
ASEL, R, LOC, X, 1, f_t-1, 1, 0
ASEL, R, LOC, Y, -d_inima-r_bulon-5 , d_inima+r_bulon+5, 1, 0
ASEL, R, LOC, Z, z1-5 , z5+5, 1, 0
NSLA,,1
ESURF

TYPE,3
VSEL,S,,,8,17,1
ASLV
NSLA,,1
NSEL,R,LOC,X,-st_tf+1,f_t-1,1
ESURF

CNCHECK,AUTO 
ALLS

/SOL
ANTYPE,0
NSUBST,10,15,5  
OUTRES,ERASE
OUTRES,ALL,1
AUTOTS,1
NEQIT,20
TIME,1 
NROP,UNSYM
SOLVE
